class JASM.console
func int init(null)
	newl(null)
	full = 0
	fullscrn(null)
	draw = 1
	file = 0
	return 0
func int post_init(null)
	color(0xF)
	print("Console: ")
	catch_enter(handleEnter)
	return 0
func int handleEnter(null)
	done = 0
	inp = getLine(null)
	newl(null)
	inp += 9
	if (file = 1)
		inp += 2
		String.mem(inp)
		View.file(inp)
		println("See debug for details.")
		file = 0
		done = 1
	endif
	if (inp seq_mem "HELP")
		color(0x3D)
		println("EXIT: Exits the console.")
		println("CLEAR: Clears the screen.")
		println("HELP: Displays this prompt.")
		println("FULLSCRN: Toggles fullscreen mode.")
		println("DEBUG: Toggles the display of debug")
		println("VIEW: View a file")
		println("TREE: Displays all mounted files")
		done = 1
	endif
	if (inp seq_mem "EXIT")
		catch_enter(os.return)
		draw = 0
		return 0
	endif
	if (inp seq_mem "CLEAR")
		cls(null)
		done = 1
	endif
	if (inp seq_mem "FULLSCRN")
		fullscrn(null)
	endif
	if (inp seq_mem "DEBUG")
		toggleDebug(null)
		done = 1
	endif
	if (inp seq_mem "VIEW")
		file = 1
		done = 1
	endif
	if (inp seq_mem "TREE")
		color(0x9)
		Minnow.tree(null)
		done = 1
	endif
	if (done = 0)
		color(0x3)
		print("Unrecognized Command: ")
		print("'")
		print(inp)
		println("'")
	endif
	color(0xF)
	if (file = 0)
		print("Console: ")
		return 0
	endif
		print("File Name: ")
		return 0
func int fullscrn(null)
	if (full = 0)
			setPos(0x0)
			setWidth(0xa0)
			setHeight(0xc8)
			full = 2
	endif
	if (full = 1)
		setPos(0x280)
		setWidth(0x140)
		setHeight(0xc8)
		full = 0
	endif	
	if (full = 2)
		full = 1
	endif
	done = 1
	return 0